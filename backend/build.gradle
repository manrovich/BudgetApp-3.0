plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.12'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.springdoc.openapi-gradle-plugin' version '1.9.0'
}

group = 'ru.manrovich'
version = '0.0.1-SNAPSHOT'
description = 'Budget Application'

java {
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.liquibase:liquibase-core'
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14"
	implementation "org.liquibase.ext:liquibase-hibernate6:5.0.1"
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.tngtech.archunit:archunit-junit5:1.2.1'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter:1.20.4'
	testImplementation 'org.testcontainers:postgresql:1.20.4'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

openApi {
	apiDocsUrl.set("http://localhost:5000/v3/api-docs.yaml")
	outputDir.set(file("$buildDir/openapi"))
	outputFileName.set("openapi.yaml")
	waitTimeInSeconds.set(30)

	customBootRun {
		args = [
				"--spring.profiles.active=openapi"
		]
	}
}

tasks.withType(JavaCompile).configureEach {
	options.release = 17
}

tasks.named('test', Test) {
	useJUnitPlatform()

	def withIntegration = project.hasProperty('withIntegration') ||
			((System.getenv('WITH_INTEGRATION_TESTS') ?: 'false').toBoolean())

	if (!withIntegration) {
		useJUnitPlatform { excludeTags 'integration' }
		logger.lifecycle("Running unit tests only (integration tests are excluded).")
	} else {
		logger.lifecycle("Running ALL tests including integration tests.")
	}
}

tasks.register('integrationTest', Test) {
	group = LifecycleBasePlugin.VERIFICATION_GROUP
	useJUnitPlatform {
		includeTags 'integration'
	}
	shouldRunAfter tasks.test
}